plugins {
    id 'java'
    id 'signing'
    id 'checkstyle'
    id 'com.vanniktech.maven.publish' version '0.34.0'
}

group = 'io.github.alexshamrai'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
    options.release = 17
    options.encoding = 'UTF-8'
}

ext {
    projectVersion = '0.3.7-SNAPSHOT'
    releaseVersion = '0.3.6'

    junitVersion = '5.13.3'
    lombokVersion = '1.18.38'
    jaksonVersion = '2.19.1'
    ownerVersion = '1.0.12'
    mockitoVersion = '5.18.0'
    assertjVersion = '3.27.3'

    slf4jVersion = '2.0.17'
    logbackVersion = '1.5.18'
    jacksonModuleJsonSchemaVersion = '2.19.1'
    jsonSchemaValidatorVersion = '2.2.14'
}

version = projectVersion

dependencies {
    implementation platform("org.junit:junit-bom:${junitVersion}")
    implementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    implementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    implementation "org.junit.platform:junit-platform-launcher"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jaksonVersion}"
    implementation "org.aeonbits.owner:owner:${ownerVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation "org.junit.platform:junit-platform-launcher"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
}

test {
    useJUnitPlatform()
}

checkstyle {
    toolVersion = '10.26.1'
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
}

mavenPublishing {
    publishToMavenCentral()
    signAllPublications()
    coordinates("io.github.alexshamrai", "junit-ctrf-extension", "${releaseVersion}")
    pom {
        name = "Junit CTRF Extension"
        description = "Junit extension to create test reports that follow the CTRF standard."
        inceptionYear = "2024"
        url = "https://github.com/alexshamrai/junit-ctrf-extension"
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = "alexshamrai"
                name = "Oleksii Shamrai"
                url = "https://github.com/alexshamrai/"
            }
        }
        scm {
            url = "https://github.com/alexshamrai/junit-ctrf-extension"
            connection = "scm:git:git://github.com/alexshamrai/junit-ctrf-extension.git"
            developerConnection = "scm:git:ssh://git@github.com/alexshamrai/junit-ctrf-extension.git"
        }
    }
}